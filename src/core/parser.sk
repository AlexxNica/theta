namespace Equation {
	enum TokenKind {
		CODE_POINT
		END_OF_INPUT
		OPERAND
		OVER
		UNDER
	}

	enum Precedence {
		LOWEST
		EQUAL
		ADD
		MULTIPLY
		PREFIX
		POSTFIX
	}

	################################################################################

	flags TokenFlags {
		IS_FUNCTION
	}

	class Token {
		const kind TokenKind
		const range Range
		const codePoint int
		const node Node
		const flags TokenFlags

		def isWord bool {
			return kind == .CODE_POINT && Glyph.isWordCodePoint(codePoint)
		}

		def isNumber bool {
			return kind == .CODE_POINT && (codePoint >= '0' && codePoint <= '9' || codePoint == '.')
		}
	}

	################################################################################

	class ParserContext {
		const log Log
		const _tokens List<Token>
		var _index = 0

		def current Token {
			return _tokens[_index]
		}

		def next Token {
			var token = current
			if _index + 1 < _tokens.count {
				_index++
			}
			return token
		}

		def peek(codePoint int) bool {
			var token = current
			return token.kind == .CODE_POINT && token.codePoint == codePoint
		}

		def eat(codePoint int) bool {
			if peek(codePoint) {
				next
				return true
			}
			return false
		}

		def hasMoreTokens bool {
			return current.kind != .END_OF_INPUT
		}

		def unexpectedToken {
			log.syntaxErrorUnexpectedToken(current)
		}
	}

	################################################################################

	class Parser {
		var _log Log
		const _platform Platform

		def parse(container Layout.Container) Node {
			return _tokenizeAndParse(container, 0, .LOWEST)
		}

		def _prefix(context ParserContext) Node {
			var token = context.current

			# Operand tokens
			if token.kind == .OPERAND {
				context.next

				# All functions must be called
				if .IS_FUNCTION in token.flags {
					var target = token.node
					var argument Node = null

					# Handle an exponent on the function itself ("sin^2 x")
					if context.current.kind == .OVER {
						var power = context.next

						# Special-case "-1" to mean calling the inverse function
						if power.node.kind == .NUMBER && power.node.asNumber == -1 {
							target = Node.createUnary(.INVERSE, token.node).withRange(Range.span(token.node.range, power.range))
						}

						# Forbid function exponent notation because it's ambiguous, confusing, and easy to work around
						else {
							_log.syntaxErrorFunctionExponentiation(power.node.range)
						}
					}

					# Check for a parenthesized operand first so "sin(x)^2" is parsed as "(sin x)^2" and not "sin(x^2)"
					if context.current.kind == .OPERAND && context.current.node.kind == .TUPLE {
						argument = context.next.node
					}

					# Handle implicit compound arguments ("sin 2x cos -3y - tan 4z*5" => "sin(2x) cos(-3y) - tan(4z)*5")
					else {
						argument = _parse(context, .MULTIPLY)
						if argument == null {
							return null
						}
						while context.current.kind == .OPERAND && !(.IS_FUNCTION in context.current.flags) {
							var right = _parse(context, .MULTIPLY)
							if right == null {
								return null
							}
							argument = _join(.MULTIPLY, argument, right)
						}
					}

					# Handle multiple arguments
					return Node.createCall(target, argument.kind == .TUPLE ? argument : Node.createTuple.appendChild(argument).withRangeFromChildren).withRangeFromChildren
				}

				return token.node
			}

			# Code point tokens
			if token.kind == .CODE_POINT {
				switch token.codePoint {
					case '+' {
						context.next
						return _parse(context, .PREFIX)
					}

					case '-' {
						context.next
						var value = _parse(context, .PREFIX)
						if value == null {
							return null
						}

						# Automatically update the sign of numeric literals in place
						return _negate(value, token.range)
					}
				}
			}

			context.unexpectedToken
			return null
		}

		def _infix(context ParserContext, left Node, precedence Precedence) Node {
			var token = context.current
			var c = token.codePoint

			# Operand tokens
			if token.kind == .OPERAND {
				var right = _parse(context, .MULTIPLY)
				if right == null {
					return null
				}
				return _join(.MULTIPLY, left, right)
			}

			# Code point tokens
			if token.kind == .CODE_POINT {
				context.next

				switch c {
					case '!' {
						return Node.createUnary(.FACTORIAL, left).withRange(Range.span(left.range, token.range))
					}

					case '*' {
						var right = _parse(context, .MULTIPLY)
						if right == null {
							return null
						}
						return _join(.MULTIPLY, left, right)
					}

					case '=', '<', '>', '≤', '≥', '÷' {
						var right = _parse(context, c == '÷' ? .MULTIPLY :
							# Relational operators are right-associative
							(Precedence.EQUAL - 1) as Precedence)
						if right == null {
							return null
						}
						return Node.createBinary(
							c == '<' ? .LESS_THAN :
							c == '>' ? .GREATER_THAN :
							c == '≤' ? .LESS_THAN_EQUAL :
							c == '≥' ? .GREATER_THAN_EQUAL :
							c == '÷' ? .DIVIDE :
							.EQUAL, left, right).withRangeFromChildren.withInternalRange(token.range)
					}

					case '+', '-' {
						var right = _parse(context, .ADD)
						if right == null {
							return null
						}
						if c == '-' {
							right = _negate(right, token.range)
						}
						return _join(.ADD, left, right)
					}
				}
			}

			# Over tokens
			if token.kind == .OVER {
				context.next
				return Node.createBinary(.EXPONENT, left, token.node).withRange(token.range)
			}

			context.unexpectedToken
			return null
		}

		def _tokenizeAndParseTuple(container Layout.Container, baseIndex int) Node {
			var context = _tokenize(container, baseIndex)
			var node = Node.createTuple

			# Parse
			while true {
				var child = _parse(context, .LOWEST)
				if child == null {
					node = Node.createError
					break
				}
				node.appendChild(child)
				if !context.eat(',') {
					break
				}
			}

			# Make sure everything was parsed
			if context.hasMoreTokens {
				context.unexpectedToken
				node = Node.createError
			}

			return node.withRange(Range.new(baseIndex - 1, baseIndex + container.anchorCount))
		}

		def _tokenizeAndParse(container Layout.Container, baseIndex int, precedence Precedence) Node {
			var context = _tokenize(container, baseIndex)
			var node = _parse(context, precedence)

			# Make sure everything was parsed
			if node != null && context.hasMoreTokens {
				context.unexpectedToken
			}

			return node ?? Node.createError.withRange(Range.new(baseIndex, baseIndex + container.anchorCount - 1))
		}

		def _isValidInfix(token Token, precedence Precedence) bool {
			switch token.kind {
				case .CODE_POINT {
					switch token.codePoint {
						case '=', '<', '>', '≤', '≥' { return precedence < Precedence.EQUAL }
						case '+', '-' { return precedence < Precedence.ADD }
						case '*', '÷' { return precedence < Precedence.MULTIPLY }
						case '!' { return precedence < Precedence.POSTFIX }
					}
				}
				case .END_OF_INPUT {}
				case .OPERAND { return precedence < Precedence.MULTIPLY }
				case .OVER, .UNDER { return precedence < Precedence.POSTFIX }
				default { assert(false) }
			}
			return false
		}

		# A Pratt parser is a parser that associates up to two operations per token,
		# each with its own precedence. Pratt parsers excel at parsing expression
		# trees with deeply nested precedence levels. For an excellent writeup, see:
		#
		#   http://journal.stuffwithstuff.com/2011/03/19/pratt-parsers-expression-parsing-made-easy/
		#
		def _parse(context ParserContext, precedence Precedence) Node {
			# Prefix
			var node = _prefix(context)
			assert(node == null || node.range != null) # All parsed nodes must have a valid range set

			# Infix
			while node != null && _isValidInfix(context.current, precedence) {
				node = _infix(context, node, precedence)
				assert(node == null || node.range != null) # All parsed nodes must have a valid range set
			}

			return node
		}

		def _tokenize(container Layout.Container, baseIndex int) ParserContext {
			var tokens List<Token> = []

			# Start off with a simple 1:1 conversion of elements to tokens
			for child in container.children {
				var nextBaseIndex = baseIndex + 1 + child.anchorCount
				var range = Range.new(baseIndex, nextBaseIndex)
				var kind TokenKind
				var codePoint = 0
				var node Node = null
				var flags TokenFlags = 0

				# Character elements
				if child is Layout.Character {
					kind = .CODE_POINT
					codePoint = (child as Layout.Character).codePoint
				}

				# Special elements
				else {
					assert(child is Layout.Special)
					var special = child as Layout.Special
					var containers = special.children

					if special.kind == .PARENTHESES {
						assert(containers.count == 1)
						kind = .OPERAND
						node = _tokenizeAndParseTuple(containers[0], baseIndex + 1)
					}

					else {
						assert(!containers.isEmpty)
						var first = _tokenizeAndParse(containers[0], baseIndex + 1, .LOWEST)

						switch special.kind {
							case .FRACTION {
								assert(containers.count == 2)
								kind = .OPERAND
								node = Node.createBinary(.DIVIDE, first, _tokenizeAndParse(containers[1], baseIndex + 1 + containers[0].anchorCount, .LOWEST)).withRange(range)
							}

							case .OVER {
								assert(containers.count == 1)
								kind = .OVER
								node = first
							}

							case .UNDER {
								assert(containers.count == 1)
								kind = .UNDER
								node = first
							}

							case .BRACKETS {
								assert(containers.count == 1)
								kind = .OPERAND
								node = first
							}

							case .ABSOLUTE_VALUE {
								assert(containers.count == 1)
								kind = .OPERAND
								node = Node.createSingleArgumentCall(Node.createSymbol("abs").withRange(range), first).withRange(range)
							}

							case .CEIL {
								assert(containers.count == 1)
								kind = .OPERAND
								node = Node.createSingleArgumentCall(Node.createSymbol("ceil").withRange(range), first).withRange(range)
							}

							case .FLOOR {
								assert(containers.count == 1)
								kind = .OPERAND
								node = Node.createSingleArgumentCall(Node.createSymbol("floor").withRange(range), first).withRange(range)
							}

							case .SQUARE_ROOT {
								assert(containers.count == 1)
								kind = .OPERAND
								node = Node.createSingleArgumentCall(Node.createSymbol("sqrt").withRange(range), first).withRange(range)
							}
						}
					}
				}

				tokens.append(Token.new(kind, range, codePoint, node, flags))
				baseIndex = nextBaseIndex
			}

			# Next, convert runs of tokens to words and numbers
			var i = 0
			while i < tokens.count {
				var token = tokens[i]

				# Check for words
				if token.isWord {
					var text = ""
					var match string = null
					var matchCount = 0

					for j in i..tokens.count {
						var other = tokens[j]

						if !other.isWord {
							break
						}

						text += string.fromCodePoint(other.codePoint)

						# Always match at least a single character word
						var canonical = Functions.canonicalFunctionName(text)
						if matchCount == 0 || canonical != null {
							match = canonical ?? text
							matchCount = j - i + 1
						}
					}

					if matchCount != 0 {
						var range = Range.span(token.range, tokens[i + matchCount - 1].range)
						token = Token.new(.OPERAND, range, 0, Node.createSymbol(match).withRange(range), matchCount != 1 ? .IS_FUNCTION : 0)
						tokens.removeRange(i, i + matchCount)
						tokens.insert(i, token)
					}
				}

				# Check for numbers
				else if token.isNumber {
					var text = ""
					var foundDot = false
					var j = i

					while j < tokens.count {
						var other = tokens[j]

						if !other.isNumber {
							break
						}

						# Only one decimal point is allowed
						if other.codePoint == '.' {
							if foundDot {
								break
							}
							foundDot = true
						}

						text += string.fromCodePoint(other.codePoint)
						j++
					}

					if text != "." {
						var range = Range.span(token.range, tokens[j - 1].range)
						token = Token.new(.OPERAND, range, 0, Node.createNumber(_platform.parseNumber(text)).withRange(range), 0)
						tokens.removeRange(i, j)
						tokens.insert(i, token)
					}
				}

				i++
			}

			# Every token stream must end in an "end of input" token
			tokens.append(Token.new(.END_OF_INPUT, Range.new(baseIndex, baseIndex), 0, null, 0))

			return ParserContext.new(_log, tokens)
		}
	}

	namespace Parser {
		def _negate(value Node, minusRange Range) Node {
			return (value.kind == .NUMBER
				? value.withNumber(-value.asNumber)
				: Node.createUnary(.NEGATE, value).withInternalRange(minusRange)).withRange(Range.span(minusRange, value.range))
		}

		def _join(kind NodeKind, left Node, right Node) Node {
			if left.kind != kind {
				left = Node.new(kind).appendChild(left).withRange(left.range)
			}
			if right.kind == kind {
				left.appendChildrenFrom(right)
			} else {
				left.appendChild(right)
			}
			return left.withRange(Range.span(left.range, right.range))
		}
	}
}
